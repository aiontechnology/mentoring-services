// Copyright 2020-2025 Aion Technology LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'maven-publish'
    id 'signing'
}

publishing {
    publications {
        mentoring(MavenPublication) {
            from components.java
            pom {
                name = 'Mentoring'
                description = 'Mentoring services project'
                url = 'https://github.com/aiontechnology/mentoring-services'
                versionMapping {
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'whitney'
                        name = 'Whitney Hunter'
                        email = 'whitney@aiontechnology.io'
                        organization = 'aiontechnology'
                        organizationUrl = 'http://aiontechnology.io'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:aiontechnology/mentoring-services.git'
                    developerConnection = 'scm:git:ssh://github.com:aiontechnology/mentoring-services.git'
                    url = 'https://github.com/aiontechnology/mentoring-services'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (version.endsWith('SNAPSHOT')) {
                url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            } else {
                url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            }
            credentials {
                username = System.getenv('OSSRH_USERNAME')
                password = System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    sign publishing.publications.mentoring
}
