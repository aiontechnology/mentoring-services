// Copyright 2020-2021 Aion Technology LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import org.gradle.api.publish.maven.MavenPublication
import org.gradle.api.artifacts.repositories.PasswordCredentials

plugins {
    id 'maven-publish'
    id 'signing'
}

publishing {
    publications {
        mentorsuccess(MavenPublication) {
            from components.java
            pom {
                name = 'MentorSuccess'
                description = 'MentorSuccess services project'
                versionMapping {
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'whitney'
                        name = 'Whitney Hunter'
                        email = 'whitney@aiontechnology.io'
                        organization = 'aiontechnology'
                        organizationUrl = 'http://aiontechnology.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://bitbucket.org:hisheartfoundation/mentorsuccess-services.git'
                    developerConnection = 'scm:git:ssh://bitbucket.org:hisheartfoundation/mentorsuccess-services.git'
                    url = 'https://bitbucket.org/hisheartfoundation/mentorsuccess-services/src/master'
                }
            }
        }
    }
    repositories {
        maven {
            credentials(PasswordCredentials) {
                username = System.getenv('ossrhUsername')
                password = System.getenv('ossrhPassword')
            }
            if (version.endsWith('SNAPSHOT')) {
                url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            } else {
                url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            }
        }
    }
}

signing {
    sign publishing.publications.mentorsuccess
}
